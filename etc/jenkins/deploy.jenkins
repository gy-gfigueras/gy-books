pipeline {
    agent any

    environment {
        NVM_DIR = "${WORKSPACE}/.nvm"

        GITHUB_USER = 'gy-gfigueras'
        GITHUB_TOKEN = credentials('FRONT_GITHUB_TOKEN')

        MAIN_REPO = 'GY-CODING/gy-books'
        FORK_REPO = "${GITHUB_USER}/gy-books"
        WORKSPACE_DIR = 'WingWords'

        VERCEL_TOKEN = credentials('VERCEL_DEPLOYMENT_TOKEN')
    }

    stages {
        stage('Prepare Repository') {
            steps {
                script {
                    if (fileExists(WORKSPACE_DIR)) {
                        echo "Repository already exists, updating..."
                        dir(WORKSPACE_DIR) {
                            sh '''
                                git reset --hard
                                git clean -fd
                                git checkout main
                                git fetch origin
                                git reset --hard origin/main
                            '''
                        }
                    } else {
                        echo "Cloning repository..."
                        sh '''
                            git clone https://$GITHUB_USER:$GITHUB_TOKEN@github.com/$FORK_REPO.git $WORKSPACE_DIR
                        '''
                    }
                }
            }
        }

        stage('Run Tests') {
            steps {
                script {
                    try {
                        build job: 'Front/Test/WingWords',
                              wait: true,
                              propagate: true
                    } catch (Exception e) {
                        echo "Error calling test job: ${e.getMessage()}"
                        throw e
                    }
                }
            }
        }

        stage('Sync Fork') {
            steps {
                script {
                    dir(WORKSPACE_DIR) {
                        sh '''
                            git remote add upstream https://github.com/$MAIN_REPO.git || true
                            git fetch upstream
                            git checkout main
                            git merge upstream/main --allow-unrelated-histories
                            git push origin main
                        '''
                    }
                }
            }
        }

        stage('Connect with Vercel') {
            steps {
                script {
                    def response = sh(script: """
                        curl -s -X POST https://api.vercel.com/v13/deployments \
                          -H "Authorization: Bearer $VERCEL_TOKEN" \
                          -H "Content-Type: application/json" \
                          -d '{
                            "name": "wingwords",
                            "gitSource": { "type": "github", "repoId": "$FORK_REPO" }
                          }'
                    """, returnStdout: true).trim()

                    def json = new groovy.json.JsonSlurper().parseText(response)
                    env.DEPLOY_ID = json.id
                    env.DEPLOY_URL = "https://${json.url}"

                    echo "ðŸ”Œ Connected with Vercel. Deployment started: ID=${env.DEPLOY_ID}, URL=${env.DEPLOY_URL}"
                }
            }
        }

        stage('Deployment') {
            steps {
                script {
                    def maxRetries = 60
                    def retryCount = 0
                    def deploymentReady = false

                    while (!deploymentReady && retryCount < maxRetries) {
                        sleep 10
                        def statusResp = sh(
                            script: """curl -s -H "Authorization: Bearer $VERCEL_TOKEN" \\
                              "https://api.vercel.com/v13/deployments/${env.DEPLOY_ID}" """,
                            returnStdout: true
                        ).trim()

                        def statusJson = new groovy.json.JsonSlurper().parseText(statusResp)
                        def state = statusJson.state

                        if (state == "READY") {
                            deploymentReady = true
                            echo "âœ… Deployment is ready: ${env.DEPLOY_URL}"
                        } else if (state == "ERROR") {
                            error("âŒ Deployment failed.")
                        } else {
                            echo "â³ Deployment still ${state}... retrying (${retryCount+1}/${maxRetries})"
                            retryCount++
                        }
                    }

                    if (!deploymentReady) {
                        error("Deployment did not complete within the expected time.")
                    }
                }
            }
        }

        stage('Create Release') {
            steps {
                script {
                    def WORKFLOW_FILE = "release.yml"

                    sh """
                        curl -X POST https://api.github.com/repos/$MAIN_REPO/actions/workflows/$WORKFLOW_FILE/dispatches \
                            -H "Accept: application/vnd.github.v3+json" \
                            -H "Authorization: token $GITHUB_TOKEN" \
                            -d '{ "ref": "main" }'
                    """
                }
            }
        }
    }
}
